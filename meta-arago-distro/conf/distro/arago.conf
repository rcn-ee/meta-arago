# Name and version of the distribution
DISTRO_NAME = "Arago"
DISTRO_VERSION = "2012.12"

# SDK paths and names
SDK_NAME_PREFIX = "arago"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_NAME = "${SDK_NAME_PREFIX}-${SDK_VERSION}"
SDKPATH = "/${SDK_NAME_PREFIX}-${SDK_VERSION}"
SDKPATHNATIVE = "${SDKPATH}"

# Mask some broken recipes
BBMASK = "meta-ti/recipes-misc|meta-linaro/recipes-lamp|meta-oe/recipes-extended/lighttpd"

# Also build 32bit Pseudo on 64bit machines, fix error when preloading libpseudo.so
NO32LIBS = "0"

# Set our distro features, don't forget all the standard libc ones
DISTRO_FEATURES = "alsa argp bluetooth ext2 irda largefile pcmcia usbgadget usbhost wifi xattr nfs zeroconf pci 3g ipv4 ipv6 pulseaudio ${DISTRO_FEATURES_LIBC}"

# Toolchain should be selected by setting TOOLCHAIN_BRAND in local.conf
# Set some sane defaults, in case someone forgets to set them in local.conf
TOOLCHAIN_BRAND ?= "arago"
TOOLCHAIN_TYPE ?= "external"

require conf/distro/include/toolchain-${TOOLCHAIN_TYPE}-${TOOLCHAIN_BRAND}.inc

# Locate the toolchain automatically from PATH
TOOLCHAIN_SYSPATH := "${@os.path.abspath(os.path.dirname(bb.which(d.getVar('PATH', d, 1), d.getVar('TOOLCHAIN_PREFIX', d, 1)+'cpp'))+'/../'+d.getVar('TOOLCHAIN_SYS', d, 1))}"
TOOLCHAIN_PATH := "${@bool(d.getVar('TOOLCHAIN_SYSPATH', d, 1)) and (os.path.exists(d.getVar('TOOLCHAIN_SYSPATH', d, 1)) and os.path.dirname(d.getVar('TOOLCHAIN_SYSPATH', d, 1)) or bb.fatal('No valid toolchain in PATH')) or ''}"
EXTERNAL_TOOLCHAIN = "${TOOLCHAIN_PATH}"

# Enable basic stack and buffer overflow protections
TARGET_CPPFLAGS += "-fstack-protector -D_FORTIFY_SOURCE=1"

PREFERRED_PROVIDER_virtual/gettext = "gettext"

PREFERRED_VERSION_qt4-embedded = "4.8.0"
PREFERRED_VERSION_qt4-native = "4.8.0"
PREFERRED_VERSION_qt4-tools-nativesdk = "4.8.0"
PREFERRED_VERSION_wpa-supplicant = "0.7.3+0.8.0-rc-ti"
PREFERRED_VERSION_hostap-daemon = "0.7.3+0.8.0-rc-ti"

# Use v4.08.00.01 of the Graphics SDK
PREFERRED_VERSION_omap3-sgx-modules = "4.08.00.01"
PREFERRED_VERSION_libgles-omap3 = "4.08.00.01"

ARAGO_QT_PROVIDER = "qt4-embedded"
# Enable Qt+GLES for ARM v7a devices
ARAGO_QT_PROVIDER_armv7a = "qt4-embedded-gles"

PREFERRED_PROVIDER_qt4-embedded = "${ARAGO_QT_PROVIDER}"

# Use the SDK supported kernel and kernel version
PREFERRED_PROVIDER_virtual/kernel_am335x-evm = "linux-am335x"
PREFERRED_VERSION_virtual/kernel_am335x-evm = "3.2.0-psp05.06.00.00"

# Use the SDK supported u-boot version
PREFERRED_VERSION_u-boot_am335x-evm = "2012.10-psp05.06.00.00"

# Pin down udev version from oe-core, as meta-oe pulls in newer systemd-enhanced version
PREFERRED_VERSION_udev = "164"
PREFERRED_PROVIDER_udev = "udev"

# Set the PREFERRED_PROVIDER for jpeg functionality based on the MACHINE
# architecture.  For armv7a devices libjpeg-turbo should be used to take
# advantage of the SIMD instructions.
PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg_armv5te = "jpeg"

# Setting PREFERRED_VERSIONS due to selecting a specific version of a library or
# application that does not have a GPLv3 license
PREFERRED_VERSION_gdbm = "1.8.3"
PREFERRED_VERSION_readline = "5.2"


# Disable polkit's systemd configuration from meta-oe
POLKITAUTH = ""

# Enable generation of source packages for SDKs
require arago-source-ipk.conf

# Whitelist several recipes that have commercial licenses
# Need to verify with legal that the following recipes are ok to distribute
# with the SDK
LICENSE_FLAGS_WHITELIST = "\
    commercial_gst-plugins-ugly \
    commercial_mpeg2dec \
    commercial_lame \
"
